[LONGITUDINAL]
input={initT, ldS, ltau, lk, lw, ldE, lE50, initV, initAP}

EQUATION:
odeType=stiff

;parameter transformations
dS = exp(ldS)    ; susceptible death rate [1/day] 
tau = exp(ltau)    ; productive infection probability []
k = exp(lk)        ; immune cell killing rate [uL/cell-day]/[uL]
w = exp(lw)       ; immune cell multiplier [1/day]
dE = exp(ldE)    ; immune death rate [1/day]
E50 = exp(lE50)

;fixed parameters
aS  = 70         ; constant growth rate of susceptibles [cells/uL/day]
Bt0 = 1e-4     ; infection rate of T-cells [uL/cells-day]
dI  = 0.6         ; infected cell death rate [1/day]
p = 5e6        ; burst rate of virus from cells [virions/cell]
g = 23          ; virus clearance rate [1/day]
lam = 1e-4     ; latency probability []
xi = 1e-5       ; latent activation rate [1/day]
thL = 5.2e-4   ; latent clearance rate [1/day]
aE = 1e-4      ; immune cell addition [cells/uL-day]

;initial conditions
t_0 = initT
S_0 = aS/dS                             ; assumes no real T cell depletion in very early infection
V_0 = initV ;0.03                                ; viral load at 30 copies/mL detection limit
I_0 = 1; V*g/p                               ; assumes at equilibrium
AU_0 = 0; tau*(1-lam)*I_0
AP_0 = initAP; (1-tau)*(1-lam)*I_0
LU_0 = 0;tau*lam*I_0
LP_0 = 0; (1-tau)*lam*I_0                 ; assumes proportions are ok
E_0 = aE/dE                                  ; assumes no real immune response before first positive?

;ordinary differential equations
ddt_S = aS - dS*S - Bt0*S*V                                                         ;susceptible cells
ddt_AU = (1-tau)*(1-lam)*Bt0*S*V - dI*AU - k*E*AU + xi*LU        ; active unproductively infected
ddt_AP = tau*(1-lam)*Bt0*S*V - dI*AP - k*E*AP + xi*LP              ; active productively infected
ddt_LU = (1-tau)*lam*Bt0*S*V + thL*LU                                       ; latent unproductively infected
ddt_LP = tau*lam*Bt0*S*V + thL*LP                    	      ; latent productively infected
ddt_E = w*E*(AP+AU)/(E+E50) + aE - dE*E;                               ; adaptive immune system
ddt_V = p*AP - g*V - Bt0*S*V   

Vout = log10(V*1e3)

OUTPUT:
output={Vout}


